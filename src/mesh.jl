# Abstract type for Polygonal Meshes
abstract type AbstractPolygonalMesh end

struct Node{N,T}
    x::SVector{N,T}
    ref::Int64
end

"""
get_coords(node::Node) = node.x
get coordinates of a node
"""
get_coordinates(node::Node) = node.x

struct Element
    nodes::Vector{Int64}
    faces::Vector{Int64}
end
struct Face
    elements::Vector{Int64}
    nodes::Vector{Int64}
    ref::Int64
end
struct PolygonalMesh{dims,Type} <: AbstractPolygonalMesh
    elements::Vector{Element}
    nodes::Vector{Node{dims,Type}}
    faces::Vector{Face}
end
get_elements(mesh::PolygonalMesh) = mesh.elements
get_nodes(mesh::PolygonalMesh) = mesh.nodes
get_faces(mesh::PolygonalMesh) = mesh.faces
node(idx::Int, face::Face, mesh::PolygonalMesh) = mesh.nodes[face.nodes[idx]]
node(idx::Int, ele::Element, mesh::PolygonalMesh) = mesh.nodes[ele.nodes[idx]]
ndims(mesh::PolygonalMesh{dims,Type}) where {dims,Type} = dims

nodes(ele::Element, mesh::PolygonalMesh{dims,Type}) where {dims,Type} = [mesh.nodes[node] for node in ele.nodes]
nodes(face::Face, mesh::PolygonalMesh{dims,Type}) where {dims,Type} = [mesh.nodes[node] for node in face.nodes]
faces(ele::Element, mesh::PolygonalMesh) = [mesh.faces[face] for face in ele.faces]
elements(face::Face, mesh::PolygonalMesh) = [mesh.elements[ele] for ele in face.elements]

function element_diameter(mesh::PolygonalMesh{N,T}, idx::Int) where {N,T}
    K = get_elements(mesh)[idx]
    h = zero(T)
     for k in K.faces
        σ = mesh.faces[k]
        mσ = norm(get_coordinates(node(2, σ, mesh)) - get_coordinates(node(1, σ, mesh)))
        h = max(h, mσ)
    end
    h
end


# Read mesh from a triangle generated file
function read_line(ln, types)
    m2 = matchall(r"\b((\d*\.)?\d+)\b", ln)
    return (parse(types[i],x) for (i,x) in enumerate(m2))
end

function parse_nodes!(nodes,root_file)
    open(root_file*".node") do f
        first_line = true
        for ln in eachline(f)
            m = match(r"^\s*(?:#|$)", ln)
            if m == nothing
                if (first_line)   #skip first line
                    first_line = false
                else
                    #parse nodes
                    pln = collect(read_line(ln, (Int,Float64,Float64,Int)))
                    node = Node(SVector(pln[2:3]...),pln[4])
                    push!(nodes,node)
                end
            end
        end
    end
end

function parse_faces!(faces,root_file)
    open(root_file*".edge") do f
        first_line = true
        for ln in eachline(f)
            m = match(r"^\s*(?:#|$)", ln)
            if m == nothing
                if (first_line)   #skip first line
                    first_line = false
                else
                    #parse nodes
                    pln = collect(read_line(ln, (Int,Int,Int,Int)))
                    face = Face(Vector{Int}(),pln[2:3],pln[4])
                    push!(faces, face)
                end
            end
        end
    end
end

function parse_elements!(elements,faces,root_file)
    all_nodes = Vector{Vector{Node}}()
    #read element nodes
    open(root_file*".ele") do f
        first_line = true
        n_el = 0
        for ln in eachline(f)
            m = match(r"^\s*(?:#|$)", ln)
            if m == nothing
                if (first_line)   #skip first line
                    first_line = false
                else
                    n_el = n_el + 1
                    #parse nodes
                    pln = collect(read_line(ln, (Int,Int,Int,Int)))
                    el_nodes = pln[2:4]
                    el_faces = [-1,-1,-1]
                    #look for faces
                    for (i,c_face) in enumerate([el_nodes[1:2],el_nodes[2:3],
                        [el_nodes[3],el_nodes[1]]])
                        for (j,face) in enumerate(faces)
                            if sort(face.nodes) == sort(c_face)
                                el_faces[i] = j
                                if !(n_el in face.elements)
                                    push!(face.elements,n_el)
                                end
                            end
                        end
                    end
                    #save element
                    element = Element(el_nodes, el_faces)
                    push!(elements, element)
                end
            end
        end
    end
end

"""
function parse_mesh_triangle(root_file)
read mesh generated by triangle with root file name `root_file`
Ex: `parse_mesh_triangle("figure.1")`
"""
function parse_mesh_triangle(root_file)
    nodes = Vector{Node}()
    faces = Vector{Face}()
    elements = Vector{Element}()
    parse_nodes!(nodes,root_file)
    parse_faces!(faces,root_file)
    parse_elements!(elements, faces, root_file)
    PolygonalMesh{size(nodes[1].x,1),eltype(nodes[1].x)}(elements, nodes, faces)
end
